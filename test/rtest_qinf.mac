/*
  quantum_information package for the Maxima computer algebra system.

  Copyright 2008, John Lapeyre  All rights reserved.

  This program is free software; you can redistribute it
  and/or modify it only under the terms of the GNU General
  Public License version 2 as published by the Free Software
  Foundation.

*/
block(
load("qinf.mac"),1
);
1;

/*****************************************************/

identitymatrixp(ident(5));
true;

is( flip(0) = 1 );
true;

is( flip(1) = 0 );
true;

identitymatrixp( mat_unblocker (genmatrix(lambda([i,j],  anticommutator(pauli[i],pauli[j])/2 ), 3,3,1,1)));
true;

/* temporarily disabled. because it needs itensor.

qtest (
  mapapply(lambda([i,j,k],zeromatrixp(commutator(pauli[i],pauli[j]) - 2*%i*levi_civita([i,j,k])*pauli[k])),
    listify(permutations([1,2,3]))),
  [true,true,true,true,true,true]
);

*/

mapapply( lambda([i,j], is(pauli[i] . ket_n(i,j) = (-1)^j * ket_n(i,j))),
    [[1,0],[1,1],[2,0],[2,1],[3,0],[3,1]  ]);
[true,true,true,true,true,true];

is(ketz(0) . braz(1) + ketz(1) . braz(0) = pauli[1]);
true;

is( -%i * ketz(0) . braz(1) + %i * ketz(1) . braz(0) = pauli[2]);
true;

is(ketz(0) . braz(0) - ketz(1) . braz(1) = pauli[3]);
true;

identitymatrixp( genmatrix(lambda([i,j], belln[i] . belln[j]  ), 3,3,0,0));
true;

identitymatrixp(apply("+",map(lambda([i],proj(belln[i])),[0,1,2,3])));
true;

is ( spinor_rotation_trig(%pi/2,%pi/2,%pi/2) . ketz(0) = ketx(0));
true;


is ( spinor_rotation_trig(%pi/2,%pi/2,-%pi/2) . ketz(0) = ketx(1));
true;

/* tovect is broken.
is ( tovect( proj(schmidt_ket(alpha))) = schmidt_ket(alpha));
true;
*/

block(
kill(alpha),
forget(alpha>0, 1-alpha>0),
pr1 : proj(schmidt_ket(alpha)),
/* require these conditions (implies that alpha is real) */
assume(alpha>0, 1-alpha>0),
/* now this returns zero as it should for a pure state */
is (entropy(pr1) = 0)
);
true;

is (ratsimp( purity(pr1) ) = 1 );
true;

is( ratsimp(ptrace(pr1,1,2)) = matrix([1]) );
true;

(
pr1a : ptrace(pr1,1),
is ( entropy(pr1a) = -alpha*log2(alpha)-log2(1-alpha)*(1-alpha) )
);
true;

is ( ratsimp(purity(pr1a)) = 2*alpha^2-2*alpha+1 );
true;

/*
block([c0,c1,c2,c3],
    is (ratsimp(invpauliexp(pauliexp(c0,c1,c2,c3))) = [c0,c1,c2,c3])
  );
true;
*/

block([c0,c1,c2,c3],
   is (ratsimp(pauliexp(invpauliexp([c0,c1,c2,c3]))) = [c0,c1,c2,c3]));
true;

block([Ughz],
  Ughz : cnot(3,2,1) . cnot(3,3,1) . insert_operator(3,[hadamard,1]),
  member(false, map( lambda( [x], is ( Ughz . apply(ketz,x) = arrayapply(ghz,x))), create_list([i,j,k],i,0,1,j,0,1,k,0,1))));
false;

block(
  member(false,mapapply(lambda([i,j,k,l], is(ptrace(proj(ghz[i,j,k]),l[1],l[2])=ident(2)/2)),create_list([i,j,k,l],i,0,1,j,0,1,k,0,1,l,[[1,2],[1,3],[2,3]]))));
false;

block(
  member(false,mapapply(lambda([i,j,k,l], is(entropy(ptrace(proj(ghz[i,j,k]),l))=1)),create_list([i,j,k,l],i,0,1,j,0,1,k,0,1,l,1,3))));
false;

block( [Ubell : cnot(2,2,1) . hadamard(2,1)],
  member(false,map( lambda( [x], is ( Ubell . apply(ketz,x) = arrayapply(bell,x))),
      create_list([i,j],i,0,1,j,0,1))));
false;
